include: resource://Neos.Fusion/Private/Fusion/Root.fusion
include: resource://Neos.Fusion.Form/Private/Fusion/Root.fusion
include: **/*.fusion

Sitegeist.Chatterbox.AssistantModuleController.index = Sitegeist.Chatterbox:Assistant.List
Sitegeist.Chatterbox.AssistantModuleController.edit = Sitegeist.Chatterbox:Assistant.Edit
Sitegeist.Chatterbox.AssistantModuleController.new = Sitegeist.Chatterbox:Assistant.New

Sitegeist.Chatterbox.AssistantModuleController.newThread = Sitegeist.Chatterbox:Assistant.NewThread
Sitegeist.Chatterbox.AssistantModuleController.showThread = Sitegeist.Chatterbox:Assistant.ShowThread

prototype(Sitegeist.Chatterbox:Assistant.List) < prototype(Neos.Fusion:Component) {
    renderer = afx`
        <legend>Available AI-Assistants</legend>
        <table class="neos-table">
            <thead>
                <tr>
                    <th>Name</th>
                    <th>Description</th>
                    <th></th>
                </tr>
            </thead>
            <tbody>
            <Neos.Fusion:Loop items={assistants} itemName="assistant">
                <tr>
                    <td>{assistant.name}</td>
                    <td>{assistant.description}</td>
                    <td class="neos-priority1 neos-aCenter">
                        <Neos.Fusion:Link.Action href.action="edit" href.arguments.assistantId={assistant.id} class="neos-button">
                            <i class="fas fa-pencil-alt icon-white"></i>
                        </Neos.Fusion:Link.Action>
                        <Neos.Fusion:Link.Action href.action="newThread" href.arguments.assistantId={assistant.id} class="neos-button">
                            <i class="fas fa-comments icon-white"></i>
                        </Neos.Fusion:Link.Action>
                    </td>
                </tr>
            </Neos.Fusion:Loop>
            </tbody>
        </table>

        <div class="neos-footer">
            <Neos.Fusion:Link.Action href.action="new" class="neos-button" >Create new AI-Assistant</Neos.Fusion:Link.Action>
        </div>

    `
}

prototype(Sitegeist.Chatterbox:Assistant.New) < prototype(Neos.Fusion:Component) {
    renderer = afx`
        <legend>Create new AI-Assistant</legend>
        <Neos.Fusion.Form:Form
            form.target.action="create"
        >
            <div class="neos-control-group">
                <label class="neos-control-label" >Name</label>
                <div class="neos-controls neos-controls-row">
                    <Neos.Fusion.Form:Input field.name="name" />
                </div>
            </div>
            <div class="neos-footer">
                <Neos.Fusion:Link.Action href.action="index" class="neos-button" >Back</Neos.Fusion:Link.Action>
                <Neos.Fusion.Form:Button attributes.class="neos-button neos-button-primary">Create</Neos.Fusion.Form:Button>
            </div>
        </Neos.Fusion.Form:Form>
    `
}


prototype(Sitegeist.Chatterbox:Assistant.Edit) < prototype(Neos.Fusion:Component) {
    renderer = afx`
        <legend>Edit AI-Assistant {assistant.name}</legend>
        <Neos.Fusion.Form:Form
            form.target.action="update"
            form.data.assistant={assistant}
        >
            <Neos.Fusion.Form:Hidden field.name="assistant[id]" />
            <Neos.Fusion.Form:Hidden field.name="assistant[model]" />

            <div class="neos-control-group">
                <label class="neos-control-label" >Name</label>
                <div class="neos-controls neos-controls-row">
                    <Neos.Fusion.Form:Input attributes.class="neos-span12" field.name="assistant[name]" />
                </div>
            </div>

            <div class="neos-control-group">
                <label class="neos-control-label" >Description</label>
                <div class="neos-controls neos-controls-row">
                    <Neos.Fusion.Form:Textarea attributes.rows="5" attributes.class="neos-span12" field.name="assistant[description]" />
                </div>
            </div>

            <div class="neos-control-group">
                <label class="neos-control-label" >Instructions</label>
                <div class="neos-controls neos-controls-row">
                    <Neos.Fusion.Form:Textarea attributes.rows="20" attributes.class="neos-span12" field.name="assistant[instructions]" />
                </div>
            </div>

            <div class="neos-control-group">
                <label class="neos-control-label" >Tools</label>
                <table class="neos-table neos-span-6">
                    <thead>
                    <tr>
                        <th></th>
                        <th>Description</th>
                    </tr>
                    </thead>
                    <tbody>
                    <Neos.Fusion:Loop items={availableTools} itemName="tool">
                        <tr>
                            <td>
                                <label class="neos-checkbox" for={"tool-" + tool.name}>
                                    <Neos.Fusion.Form:Checkbox attributes.id={"tool-" + tool.name} field.name="assistant[selectedTools]" field.multiple field.value={tool.name} />
                                    <span></span>
                                </label>
                            </td>
                            <td>
                                {tool.description}

                            </td>
                        </tr>
                    </Neos.Fusion:Loop>
                    </tbody>
                </table>
            </div>

            <div class="neos-control-group">
                <label class="neos-control-label" >Sources of knowledge</label>
                <div class="neos-controls neos-controls-row">
                    <Neos.Fusion.Form:Select attributes.rows="20" attributes.class="neos-span12" field.name="assistant[selectedSourcesOfKnowledge]" field.multiple>
                        <Neos.Fusion:Loop items={availableSourcesOfKnowledge} itemName="source" >
                            <Neos.Fusion.Form:Select.Option option.value={source.name}>{source.description}</Neos.Fusion.Form:Select.Option>
                        </Neos.Fusion:Loop>
                    </Neos.Fusion.Form:Select>
                </div>
            </div>

            <div class="neos-footer">
                <Neos.Fusion:Link.Action href.action="index" class="neos-button" >Back</Neos.Fusion:Link.Action>
                <Neos.Fusion.Form:Button attributes.class="neos-button neos-button-primary">Save</Neos.Fusion.Form:Button>
            </div>

        </Neos.Fusion.Form:Form>
    `
}

prototype(Sitegeist.Chatterbox:Assistant.NewThread) < prototype(Neos.Fusion:Component) {
    renderer = afx`
        <legend>Start a new thread</legend>
        <Neos.Fusion.Form:Form
            form.target.action="createThread"
        >
            <Neos.Fusion.Form:Hidden field.name="assistantId" field.value={assistantId} />
            <div class="neos-control-group">
                <label class="neos-control-label">Message</label>
                <div class="neos-controls neos-controls-row">
                    <Neos.Fusion.Form:Input field.name="message" attributes.class="neos-span12" />
                </div>
            </div>
            <div class="neos-footer">
                <Neos.Fusion:Link.Action href.action="index" class="neos-button" >Back</Neos.Fusion:Link.Action>
                <Neos.Fusion.Form:Button attributes.class="neos-button neos-button-primary">Start Thread</Neos.Fusion.Form:Button>
            </div>
        </Neos.Fusion.Form:Form>
    `
}

prototype(Sitegeist.Chatterbox:Assistant.ShowThread) < prototype(Neos.Fusion:Component) {
    renderer = afx`
        <legend>Show Thread {threadId}</legend>

        <Neos.Fusion:Loop items={messages} itemName="message">
            <div style={['margin: 20px 0;', (message.role == 'user') ? 'padding-left:0px;' : 'padding-left:50px;']}>
                <p>{message.role}</p>
                <span style={['min-width: 460px; padding:10px; border-radius: 10px; display:inline-block;', (message.role == 'user') ? 'background-color: #00b5ff;' : 'background-color: grey;']}>
                    <Neos.Fusion:Loop items={message.content} itemName="messageContent">
                    <pre @if={messageContent.type == 'text'}>{messageContent.text.value}</pre>
                    </Neos.Fusion:Loop>
                </span>
                <br/>
            </div>
            <pre @if={message.metadata}>{Json.stringify(message.metadata)}</pre>
        </Neos.Fusion:Loop>

        <Neos.Fusion.Form:Form
            form.target.action="addThreadMessage"
        >
            <Neos.Fusion.Form:Hidden field.name="threadId" field.value={threadId} />
            <Neos.Fusion.Form:Hidden field.name="assistantId" field.value={assistantId} />
            <div class="neos-control-group">
                <label class="neos-control-label">Message</label>
                <div class="neos-controls neos-controls-row">
                    <Neos.Fusion.Form:Input field.name="message" attributes.class="neos-span12" />
                </div>
            </div>
            <div class="neos-footer">
                <Neos.Fusion:Link.Action href.action="index" class="neos-button" >Back</Neos.Fusion:Link.Action>
                <Neos.Fusion.Form:Button attributes.class="neos-button neos-button-primary">Post Message</Neos.Fusion.Form:Button>
            </div>
        </Neos.Fusion.Form:Form>
    `
}
