prototype(Sitegeist.Chatterbox:Thread.Show) < prototype(Neos.Fusion:Component) {
    renderer = afx`
        <legend>Show Thread {threadId}</legend>
        <Neos.Fusion:Loop items={messages} itemName="message">
            <div style={['margin: 20px 0;', (message.role == 'user') ? 'padding-left:0px;' : 'padding-left:50px;']}>
                <p>{message.role}</p>
                <span style={['min-width: 460px; padding:10px; border-radius: 10px; display:inline-block;', (message.role == 'user') ? 'background-color: #00b5ff;' : 'background-color: grey;']}>
                    <Neos.Fusion:Loop items={message.content} itemName="contentPart">
                        {contentPart.value}
                    </Neos.Fusion:Loop>
                    <Neos.Fusion:Fragment @if={message.quotations && !message.quotations.empty}>
                        <hr/>
                        <ol>
                            <Neos.Fusion:Loop items={message.quotations} itemName="quotation">
                                <li style="list-style:circle; margin-left: 20px;">{quotation.name} {quotation.isPartOf}</li>
                            </Neos.Fusion:Loop>
                        </ol>
                    </Neos.Fusion:Fragment>
                </span>
                <br/>
            </div>
            <pre @if={message.metadata}>{Json.stringify(message.metadata)}</pre>
        </Neos.Fusion:Loop>

        <pre @if={metadata}>{Json.stringify(metadata)}</pre>

        <Neos.Fusion.Form:Form
            form.target.action="addThreadMessage"
            attributes.id="addThreadMessage"
            attributes.onsubmit="setTimeout('document.getElementById(\"addThreadMessageButton\").disabled=true;document.getElementById(\"addThreadMessageInput\").disabled=true;', 100);"
            form.data.organizationId={organizationId}
            form.data.threadId={threadId}
            form.data.assistantId={assistantId}
        >
            <Neos.Fusion.Form:Hidden field.name="organizationId" />
            <Neos.Fusion.Form:Hidden field.name="threadId" />
            <Neos.Fusion.Form:Hidden field.name="assistantId" />
            <div class="neos-control-group">
                <label class="neos-control-label">Message</label>
                <div class="neos-controls neos-controls-row">
                    <Neos.Fusion.Form:Input field.name="message" attributes.class="neos-span12" attributes.id="addThreadMessageInput" />
                </div>
            </div>
            <div class="neos-footer">
                <Neos.Fusion:Link.Action href.action="index" href.arguments.organizationId={organizationId} class="neos-button" >Back</Neos.Fusion:Link.Action>
                <Neos.Fusion:Link.Action href.action="showThread" href.arguments.organizationId={organizationId} href.arguments.assistantId={assistantId} href.arguments.threadId={threadId} class="neos-button" >Reload Thread</Neos.Fusion:Link.Action>
                <Neos.Fusion.Form:Button attributes.class="neos-button neos-button-primary" attributes.id="addThreadMessageButton">Post Message</Neos.Fusion.Form:Button>
            </div>
        </Neos.Fusion.Form:Form>
    `
}
