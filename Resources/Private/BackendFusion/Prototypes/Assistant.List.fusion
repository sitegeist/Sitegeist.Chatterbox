prototype(Sitegeist.Chatterbox:Assistant.List) < prototype(Neos.Fusion:Component) {
    renderer = afx`
        <legend>Available Organizations</legend>

        <Neos.Fusion.Form:Form
           form.target.action="index"
           form.data.organizationId={organizationId}
           attributes.class="neos-inline"
           form.method="get"
        >
            <Neos.Fusion.Form:Select
                field.name={"organizationId"}
                attributes.onchange="this.form.submit()"
            >
                <Neos.Fusion:Loop items={organizations} itemName="organization">
                    <Neos.Fusion.Form:Select.Option option.value={organization.id}>{organization.label}</Neos.Fusion.Form:Select.Option>
                </Neos.Fusion:Loop>
            </Neos.Fusion.Form:Select>
        </Neos.Fusion.Form:Form>
        <legend>Available AI-Assistants</legend>
        <table class="neos-table">
            <thead>
            <tr>
                <th>Name</th>
                <th>Description</th>
                <th>Instructions</th>
                <th>Model</th>
                <th></th>
            </tr>
            </thead>
            <tbody>
            <Neos.Fusion:Loop items={assistants} itemName="assistant">
                <tr>
                    <td>{assistant.name}</td>
                    <td>{assistant.description}</td>
                    <td>{String.cropAtWord(assistant.instructions, 100, '...')}</td>
                    <td>{assistant.model}</td>
                    <td class="neos-priority1 neos-aCenter">
                        <Neos.Fusion:Link.Action href.action="edit" href.arguments.assistantId={assistant.id.value} href.arguments.organizationId={organizationId} class="neos-button">
                            <i class="fas fa-pencil-alt icon-white"></i>
                        </Neos.Fusion:Link.Action>
                        <Neos.Fusion:Link.Action href.action="newThread" href.arguments.assistantId={assistant.id.value} href.arguments.organizationId={organizationId} class="neos-button">
                            <i class="fas fa-comments icon-white"></i>
                        </Neos.Fusion:Link.Action>
                    </td>
                </tr>
            </Neos.Fusion:Loop>
            </tbody>
        </table>

        <div class="neos-footer">
            <Neos.Fusion:Link.Action href.action="new" href.arguments.organizationId={organizationId} class="neos-button">Create new AI-Assistant</Neos.Fusion:Link.Action>
        </div>

    `
}
